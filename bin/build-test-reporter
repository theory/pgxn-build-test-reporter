#!/usr/bin/env perl -w

use strict;
use warnings;
use v5.30;

Reporter->new(@ARGV)->go;

package Reporter;
use strict;
use warnings;
use v5.30;
use IPC::Run qw(run);
use Config ();
use Symbol qw(gensym);
use JSON;
use TOML::Parser;
use Getopt::Long;

sub _run;

sub new {
    say "# Setup";
    Getopt::Long::Configure(qw(no_ignore_case bundling));
    my $self = {
        input_file  => 'data/distributions.txt',
        output_file => 'data/results.txt',
        pg_config   => $ENV{PG_CONFIG},
    };
    Getopt::Long::GetOptions(
        @_,
        'input-file|in=s'   => \$self->{input_file},
        'output-file|out=s' => \$self->{output_file},
        'verbose|v+'        => \$self->{verbose},
    ) or die "Usage: $0 --in INPUT_FILE --out OUTPUT_FILE\n";

    $self->{pg_config} ||= _find_pg_config();
    say "## Using $self->{pg_config}";

    $self->{pg_version} = _pg_version($self->{pg_config});
    say "## Using PostgreSQL $self->{pg_version}\n";

    bless $self => __PACKAGE__;
}

sub go {
    my $self = shift;
    say encode_json $self->build('semver', '0.32.1');
}

sub build {
    my ($self, $name, $version) = @_;
    my $record = {
        package  => $name,
        version  => $version,
        os       => $^O,
        postgres => $self->{pg_version},
        arch     => $Config::Config{myarchname},
        success  => JSON::false,
        steps    => [],
    };
    if (-e 'Cargo.toml') {
        say "## Building $name $version with pgrx";
        _run $record, 'ls -lAh', qw(ls -lAh);
        $self->pgrx_build($record);
    } elsif (-e 'Makefile' || -e 'makefile' || -e 'GNUmakefile' || -e 'Configure' || -e 'configure') {
        say "## Building $name $version with pgxs";
        _run $record, 'ls -lAh', qw(ls -lAh);
        $self->pgxs_build($record);
    } else {
        $record->{reason} = "Could not determine build pipeline";
    }
    print "\n";
    return $record;
}

sub pgxs_build {
    my ($self, $rec) = @_;
    $rec->{pipeline} = 'pgxs';
    if (-e 'Configure') {
        return unless _run $rec, 'configure', qw(./Configure)
    } elsif (-e 'configure') {
        return unless _run $rec, 'configure', qw(./configure)
    }
    my @vars = ("PG_CONFIG=$self->{pg_config}");
    return unless _run $rec, 'make', qw(make), @vars;
    return unless _run $rec, 'make install', qw(make install), @vars;
    return unless _run $rec, 'make installcheck', qw(make installcheck), @vars;
    $rec->{success} = JSON::true;
}

sub pgrx_build {
    my ($self, $rec) = @_;

    local $ENV{CARGO_TERM_COLOR} = 'never';
    local $ENV{NO_COLOR} = 'true';

    $rec->{pipeline} = 'pgrx';
    my $cfg = TOML::Parser->new->parse_file('Cargo.toml');

    # Look for pgrx.
    my $pgrxv = $cfg->{dependencies}{pgrx} || die "pgrx not found in Cargo.toml\n";
    $pgrxv =~ s/^=//;

    return unless _run $rec, 'cargo install cargo-pgrx',
        qw(cargo install --locked cargo-pgrx --version), $pgrxv;

    return unless _run $rec, 'cargo pgrx init',
        qw(cargo pgrx init), "--pg$self->{pg_version}=$self->{pg_config}";

    return unless _run $rec, 'cargo pgrx package',
        qw(cargo pgrx package --test --pg-config), $self->{pg_config};

    return unless _run $rec, 'cargo test',
        qw(cargo test --all --no-default-features --features),
        "pg$self->{pg_version} pg_test", qw(-- --nocapture);

    $rec->{success} = JSON::true;
}

sub _run {
    my ($rec, $name, @cmd) = @_;
    print "### $name...";
    run \@cmd, \undef, '>pipe', (my $pipe = gensym), '2>&1';
    push @{ $rec->{steps} } => {
        name    => $name,
        command => \@cmd,
        exit    => $? >> 8,
        output  => [ map { chomp; $_ } <$pipe> ],
    };

    if ($? == 0) {
        say "ok";
        return 1
    }

    say "failed";
    $rec->{reason} = "$name failed";
    return 0;
}

sub _find_pg_config {
    run [qw(which pg_config)], \undef, \my $out
        or die "pg_config not found; did you forget to run pg-start?\n";
    chomp $out;
    return $out;
}

sub _pg_version {
    my $pg_config = shift;
    run [$pg_config, '--version'], \undef, \my $out or exit $? >> 8;
    $out =~ /(\d+)/ or die "No version found in $pg_config --version output:\n$out";
    return $1;
}
